if (isBuildModule.toBoolean()) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}

apply plugin: 'com.jakewharton.butterknife' // 插件依赖

android {
    compileSdkVersion rootProject.ext.android.compileSdkVersion

    defaultConfig {
        if (isBuildModule.toBoolean()) {
            applicationId rootProject.ext.android.applicationId
        }
        minSdkVersion rootProject.ext.android.minSdkVersion
        targetSdkVersion rootProject.ext.android.targetSdkVersion
        versionCode rootProject.ext.android.versionCode
        versionName rootProject.ext.android.versionName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [moduleName: project.getName()]
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        abortOnError false //如果发现错误，lint工具是否应该退出这个程序。true表示退出。
    }

    resourcePrefix 'home_'//資源文件 添加前綴
    sourceSets {
        main {
            if (isBuildModule.toBoolean()) {
                manifest.srcFile 'src/main/debug/AndroidManifest.xml'
            } else {
                manifest.srcFile 'src/main/release/AndroidManifest.xml'
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation 'com.android.support.constraint:constraint-layout:1.1.0'
    androidTestImplementation rootProject.ext.dependencies["test-runner"]
    androidTestImplementation rootProject.ext.dependencies["espresso-core"]
    testImplementation rootProject.ext.dependencies["junit"]
    implementation rootProject.ext.dependencies["constraint_layout"]

    //library
    implementation project(':LibraryBCore')
    implementation project(':LibraryCommon')

    //Base
    implementation rootProject.ext.dependencies["appcompat-v7"]

    //ButterKnife
    implementation rootProject.ext.dependencies["butterknife"]
    // 编译的时候生成代码（如 1）
    annotationProcessor rootProject.ext.dependencies["butterknife-compiler"]

    //ARouter
    implementation(rootProject.ext.dependencies["aRouter-api"]) {
        exclude group: 'com.android.support'//排除support依赖包
    }
    annotationProcessor rootProject.ext.dependencies["aRouter-compiler"]

    //rxJava 的依赖
    implementation rootProject.ext.dependencies["rxjava2"]
}
